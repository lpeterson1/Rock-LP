^FX Child Label (Icon) ^FS

^FX FOx,y = Field Origin (upper left corner)
FTx,y = Field Typeset origin (lower left corner)
Label origin (0,0) is upper left corner of label
A0,h,w specifies font (0) and max char height and width
FB333,3,R is a 333-dot-wide 3-line right justified text block
FD & FS are start and end of field data
XA and XZ begin and end label a format
Comments from FX to FS may be removed to optimize label ^FS

^FX Configure printer (change JSx to JSN: tear-off, JSB: cutter) ^FS
^XA~TA000~JSx^LT0^MNW^MTD^PON^PMN^LH0,0^JMA^PR6,6~SD15^LRN^CI28^XZ

^XA
^FX Change to set cutter mode MMC: cutter, MMT: tear-off ^FS
^MMx

^FX Print width & length ^FS
^PW812
^LL0406

^FX Set "Z" font to Rock icon font ^FS
^CWZ,E:ROC000.FNT

^FX Security code ^FS
^FO470,19^A0,135,134^FB333,,R^FDWWW^FS

^FX Nickname ^FS
^FT12,254^A0,135,146^FD5^FS

^FX Last name ^FS
^FT14,309^A0,45,45^FD6^FS

^FX Birthday & first time icons ^FS
^FT263,82^AZ,73,73^FDB^FS
^FT348,81^AZ,73,73^FDF^FS

^FX Allergy & legal icons ^FS
^FT605,383^AZ,73,73^FD7^FS
^FT715,386^AZ,73,73^FD8^FS

^FX Birthday day of week ^FS
^FT270,120^A0,28,28^FD4^FS

^FX Person locations/times (2 lines) ^FS
^FB330,2^FT8,382^A0,28,28^FD9^FS

^FX Black bar ^FS
^FR^FO0,0^GB820,0,136^FS
^XZ
